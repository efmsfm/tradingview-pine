// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © liruizhao54

//@version=5
indicator(title="[rusice]Channel/EMA/Tips", shorttitle="Channel/Tips", overlay=true)
// *****************************************************************************
// | Basic
// *****************************************************************************
showATRChannel              =               input.bool(title="显示ATR通道", defval=false)
showBLChannel               =               input.bool(title="显示布林带通道", defval=false)
atr_length                  =               input.int(title="ATR周期长度", defval=19)
distance                    = input(3, "Label Distance")
r1_x                        = time + math.round(ta.change(time) * distance)

// *****************************************************************************
// | 在指标旁边显示Label的方法
// | label_txt<String> 文本
// | label_y<Plot> 坐标点（Y）
// *****************************************************************************
disLabel(label_txt, label_y) =>
    emalabel = label.new(x=r1_x, y=label_y, text=label_txt, xloc=xloc.bar_time, yloc=yloc.price, style=label.style_none, textcolor=color.black, size=size.small)
    label.delete(emalabel[1])

// *****************************************************************************
// | EMA均线
// *****************************************************************************
ema_short                   =               ta.ema(close, 20)
ema_middle                  =               ta.ema(close, 50)
ema_100                     =               ta.ema(close, 100)
ema_long                    =               ta.ema(close, 200)
ema_short_plot              =               plot(ema_short, color=color.black, title="短周期EM", linewidth=1)
plot(ema_middle, color=color.black, title="EMA50", linewidth=1)
plot(ema_100, color=color.black, title="EMA100", linewidth=2)
plot(ema_long, color=color.black, title="EMA200", linewidth=3)

disLabel("EMA 20", ema_short)
disLabel("EMA 50", ema_middle)
disLabel("EMA 100", ema_100)
disLabel("EMA 200", ema_long)

// *****************************************************************************
// | ATR
// *****************************************************************************		
atr                         =               ta.atr(atr_length)
timeATR                     =               atr * 2

// *****************************************************************************
// | 凯特勒通道
// *****************************************************************************
c_up                        =               ema_short + timeATR
c_down                      =               ema_short - timeATR
atr_uper                    =               plot(c_up, title="凯特勒通道上边界", color=showATRChannel ? color.black : na, linewidth= showATRChannel ? 1 :na)
atr_dner                    =               plot(c_down, title="凯特勒通道下边界", color=showATRChannel ? color.black : na, linewidth= showATRChannel ? 1 : na)
fill(atr_uper, atr_dner, title="凯特勒通道", color= showATRChannel ? color.new(color.red, transp=80) : na)

// *****************************************************************************
// | 布林带
// *****************************************************************************
long_boll_mult              =               input.float(title="布林带偏差", defval=2)
boll_mid                    =               ta.sma(close, 20)
long_dev                    =               long_boll_mult * ta.stdev(close, 20)
l_upper                     =               boll_mid + long_dev
l_lower                     =               boll_mid - long_dev
l_b_p1                      =               plot(l_upper, "BL Upper", color=showBLChannel ? color.new(color.black, transp = 50) : na, linewidth= showBLChannel ? 1 : na)
l_b_p2                      =               plot(l_lower, "BL Lower", color=showBLChannel ? color.new(color.black, transp = 50) : na, linewidth= showBLChannel ? 1 : na)
fill(l_b_p1, l_b_p2, title="BL通道", color=showBLChannel ? color.new(color.gray, transp = 90) : na)

// *****************************************************************************
// | 右上角提示文字
// *****************************************************************************
tips = array.new<string>()
array.push(tips, "================开单顺序================")
array.push(tips, "1.吃掉【流动性】并突破")
array.push(tips, "2.【形状转变】找入场")
array.push(tips, "3.【不平衡区域】找出场")
array.push(tips, "================叠BUFF==================")
array.push(tips, "K线：长影、吞没、头肩底/顶")
array.push(tips, "形态：持续、反转、均线")
array.push(tips, "信号：背离")
array.push(tips, "1/4小时看方向，15分钟找流动性，2-5分钟找入场")
// *如果还需要添加，则复制上一行，改变中文部分文字即可

var testTable = table.new(position = position.top_right, columns = 1, rows = array.size(tips), bgcolor = #fefdce)
for [tip_index, tip] in tips
    table.cell(table_id = testTable, column = 0, row = tip_index, text = tip, text_size = size.small )
    